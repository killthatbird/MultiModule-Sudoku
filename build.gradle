buildscript {
    ext {
        // SpringBoot framework
        springBootVersion = "1.5.3.RELEASE"// 2017,0505

        // Others
        // mySql
        // mysql-connector-java
        mysqlVersion = "6.0.6"// 2017,0313
        // Lombok
        lombokVersion = "1.16.16"// 2017,0326
        // Flyway
        flywayVersion = "4.2.0"// 2017,0505
        // Quartz
        quartzVersion = "2.3.0"// 2017,0505
        // EclipseCollections
        ecVersion = "8.1.0"// 2017,0326
        // CheckerFramework
        checkerframeworkVersion = "2.1.11"// 2017,0505
        // Selenide
        selenideVersion = "4.4.3"// 2017,0505
        // Selenium
        seleniumVersion = "3.4.0"// 2017,0505
    }
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'jacoco'
    id 'checkstyle'
    id 'findbugs'
    id 'pmd'
    id 'project-report'
    id 'war'
    id "com.gradle.build-scan" version "1.7.1"
    id "com.moowork.node" version "1.1.1"
    id "com.moowork.gulp" version "1.1.1"
    id "org.sonarqube" version "2.4"
    id "org.springframework.boot" version "1.5.3.RELEASE"
}

allprojects {
    version = "0.0.0"
}

subprojects {

    apply plugin: 'org.springframework.boot'

    // ソースと実行ファイルのJava バージョン
    def jdkVersion = 1.8
    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
    def defaultEncoding = 'UTF-8'
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

    javadoc {
        options.charSet = defaultEncoding
        options.encoding = defaultEncoding
        options.addBooleanOption('Xdoclint:none', true)
    }

    springBoot {
        executable = true
    }

    repositories {
        jcenter()
    }

    configurations {
        checkerFrameworkAnnotatedJDK
        annotationProcessors
    }

    dependencies {

        // SpringBoot projects
        compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-aop:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-batch:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-cache:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-logging:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-parent:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-security:$springBootVersion")
        testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion")
        runtime("org.springframework.boot:spring-boot-starter-tomcat:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")

        compile("org.springframework.boot:spring-boot-devtools:$springBootVersion")

        // Others
        // JDBC Drivers
        runtime("mysql:mysql-connector-java:$mysqlVersion")
        // Lombok
        annotationProcessors("org.projectlombok:lombok:$lombokVersion")
        compileOnly("org.projectlombok:lombok:$lombokVersion")
        // Flyway
        compile("org.flywaydb:flyway-core:$flywayVersion")
        // Quartz
        compile("org.quartz-scheduler:quartz:$quartzVersion")
        compile("org.quartz-scheduler:quartz-jobs:$quartzVersion")
        // EclipseCollections
        compile("org.eclipse.collections:eclipse-collections:$ecVersion")
        compile("org.eclipse.collections:eclipse-collections-api:$ecVersion")
        testCompile("org.eclipse.collections:eclipse-collections-testutils:$ecVersion")
        // CheckerFramework
        checkerFrameworkAnnotatedJDK("org.checkerframework:jdk8:$checkerframeworkVersion")
        annotationProcessors("org.checkerframework:checker:$checkerframeworkVersion")
        compileOnly("org.checkerframework:checker-qual:$checkerframeworkVersion")
        compile("com.google.code.findbugs:jsr305:3.0.1")

        // Selenide
        testCompile("com.codeborne:selenide:$selenideVersion")
        // Selenium
        testCompile("org.seleniumhq.selenium:selenium-server:$seleniumVersion")
        // SafariDriver
        testCompile("org.seleniumhq.selenium:selenium-safari-driver:$seleniumVersion")
        // ModelMapper
        compile("com.github.jmnarloch:modelmapper-spring-boot-starter:1.1.0")

    }

    compileJava {
        def processors = [
                'org.checkerframework.checker.nullness.NullnessChecker',
                'lombok.launch.AnnotationProcessorHider\$AnnotationProcessor',
        ]
// CheckerFramework 不具合
//        options.compilerArgs = [
//                '-processor', processors.join(','),
//                '-processorpath', "${configurations.annotationProcessors.asPath}",
//                '-AprintErrorStack',
//                "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}",
//                '-Xlint:unchecked'
//        ]
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.annotationProcessors]
        }
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}

sonarqube {
    properties {
        property "sonar.host.url", "http://localhost:9000/"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    }
}

checkstyle {
    configFile file('downloaded_google_checks.xml')
    showViolations = true
}

pmd {
    ruleSets = [
            'java-braces',
            'java-codesize',
    ]
}

project(':micro-batch') {
    dependencies {
        compile project(':micro-core')
    }
}

project(':micro-core') {
    dependencies {
    }
}
project(':micro-web') {
    dependencies {
        compile project(':micro-core')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}