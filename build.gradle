buildscript {
    ext {
        // SpringBoot framework
        springBootVersion = "1.4.3.RELEASE"// 2016,1223

        // Other
        // mySql
        // mysql-connector-java
        mysqlVersion = "6.0.5"// 2016,1113
        // Lombok
        lombokVersion = "1.16.12"// 2016,1203
        // Flyway
        flywayVersion = "4.0.3"// 2017,0103
        // Quartz
        quartzVersion = "2.2.3"// 2017,0103
        // EclipseCollections
        ecVersion = "8.0.0"// 2016,1011
        // CheckerFramework
        checkerframeworkVersion = "2.1.7"// 2017,0108
        // Selenide
        selenideVersion = "4.2"// 2016,1226
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework:springloaded:1.2.6.RELEASE")
    }
}

plugins {
    id 'java'
    id 'application'
    id 'idea'
    id 'jacoco'
    id 'checkstyle'
    id 'findbugs'
    id 'pmd'
    id 'project-report'
    id 'war'
    id "com.moowork.grunt" version "1.0.1"
    id "com.moowork.node" version "1.0.1"
    id "org.sonarqube" version "2.2.1"
    id "org.springframework.boot" version "1.4.3.RELEASE"
}

allprojects {
    version = "0.0.0"
}

subprojects {

    apply plugin: 'org.springframework.boot'

    // ソースと実行ファイルのJava バージョン
    def jdkVersion = 1.8
    sourceCompatibility = jdkVersion
    targetCompatibility = jdkVersion
    def defaultEncoding = 'UTF-8'
    tasks.withType(AbstractCompile).each { it.options.encoding = defaultEncoding }

    javadoc {
        options.charSet = defaultEncoding
        options.encoding = defaultEncoding
        options.addBooleanOption('Xdoclint:none', true)
    }

    springBoot {
        executable = true
    }

    repositories {
        jcenter()
    }

    configurations {
        checkerFrameworkAnnotatedJDK
        annotationProcessors
    }

    ext['thymeleaf.version'] = '3.0.3.RELEASE'// 2016,1205
    ext['thymeleaf-layout-dialect.version'] = '2.1.2'// 2016,1220
    ext['thymeleaf-extras-java8time.version'] = '3.0.0.RELEASE'
    ext['thymeleaf-extras-data-attribute.version'] = '2.0.1'
    dependencies {

        // SpringBoot projects
        compile("org.springframework.boot:spring-boot-starter-actuator:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-aop:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-batch:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-cache:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-data-jpa:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-logging:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-parent:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-security:$springBootVersion")
        testCompile("org.springframework.boot:spring-boot-starter-test:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion")
        compile("org.springframework.boot:spring-boot-starter-web:$springBootVersion")

        // Others
        // JDBC Drivers
        runtime("mysql:mysql-connector-java:$mysqlVersion")
        // Lombok
        annotationProcessors("org.projectlombok:lombok:$lombokVersion")
        compileOnly("org.projectlombok:lombok:$lombokVersion")
        // Flyway
        compile("org.flywaydb:flyway-core:$flywayVersion")
        // Quartz
        compile("org.quartz-scheduler:quartz:$quartzVersion")
        compile("org.quartz-scheduler:quartz-jobs:$quartzVersion")
        // EclipseCollections
        compile("org.eclipse.collections:eclipse-collections:$ecVersion")
        compile("org.eclipse.collections:eclipse-collections-api:$ecVersion")
        // CheckerFramework
        checkerFrameworkAnnotatedJDK("org.checkerframework:jdk8:$checkerframeworkVersion")
        annotationProcessors("org.checkerframework:checker:$checkerframeworkVersion")
        compileOnly("org.checkerframework:checker-qual:$checkerframeworkVersion")
        compile("com.google.code.findbugs:jsr305:3.0.1")

        // Selenide
        testCompile("com.codeborne:selenide:$selenideVersion")

        compile("com.github.jmnarloch:modelmapper-spring-boot-starter:1.1.0")

    }

    compileJava {
        def processors = [
                'org.checkerframework.checker.nullness.NullnessChecker',
                'lombok.launch.AnnotationProcessorHider\$AnnotationProcessor',
        ]
// CheckerFramework 不具合
//        options.compilerArgs = [
//                '-processor', processors.join(','),
//                '-processorpath', "${configurations.annotationProcessors.asPath}",
//                '-AprintErrorStack',
//                "-Xbootclasspath/p:${configurations.checkerFrameworkAnnotatedJDK.asPath}",
//                '-Xlint:unchecked'
//        ]
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.annotationProcessors]
        }
    }
}

sonarqube {
    properties {
        property "sonar.host.url", "http://localhost:9000/"
        property "sonar.jdbc.driverClassName", "com.mysql.jdbc.Driver"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
    }
}

pmd {
    ruleSets = [
            'java-braces',
            'java-codesize',
    ]
}

project(':micro-batch') {
    dependencies {
        compile project(':micro-core')
    }
}

project(':micro-web') {
    dependencies {
        compile project(':micro-core')
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}